///////////////////////////////////////////////////////////////////////////////
//
// Keyboard interface for up/down/left/right arrow keys
//
///////////////////////////////////////////////////////////////////////////////


%define update_keyboard ()
    x_force = right_pressed - left_pressed
    y_force = up_pressed - down_pressed
%end

group 'Keyboard variables' {

    var x_force = 0
    var y_force = 0

    var right_pressed = 0 {
        update_keyboard ()
    }
    var left_pressed = 0 {
        update_keyboard ()
    }
    var up_pressed = 0 {
        update_keyboard ()
    }
    var down_pressed = 0 {
        update_keyboard ()
    }
}

%define keyboard_device ()
    iodevice/usbhid_generic (
        usage_page = 1
        usage = 6
        preferred_location_id = 2152726528  // macbook laptop
        // log_all_input_values = true
        )
%end

%define keyboard_name_to_usage = {
    'right_arrow': 79,
    'left_arrow': 80,
    'down_arrow': 81,
    'up_arrow': 82
}

%define keyboard_channel (key, value)
    iochannel/usbhid_generic_input_channel (
        usage_page = 7
        usage = keyboard_name_to_usage[key]
        value = value
        )
%end

keyboard_device controller {
    keyboard_channel (key = 'right_arrow'; value = right_pressed)
    keyboard_channel (key = 'left_arrow'; value = left_pressed)
    keyboard_channel (key = 'up_arrow'; value = up_pressed)
    keyboard_channel (key = 'down_arrow'; value = down_pressed)
}